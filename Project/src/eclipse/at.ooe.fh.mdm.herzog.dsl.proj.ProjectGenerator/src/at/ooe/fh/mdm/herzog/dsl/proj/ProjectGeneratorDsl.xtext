grammar at.ooe.fh.mdm.herzog.dsl.proj.ProjectGenerator with org.eclipse.xtext.common.Terminals

generate projectGenerator "http://www.ooe.at/fh/mdm/herzog/dsl/proj/ProjectGenerator"

Module:
    'module' name=ID '{'
    'key' key=STRING';'
    'cdiEnabled' cdiEnabled=Boolean';'
    ('messageBundles' '{' (messageBundles+=Localized+) '}' ';')?
    ('observers' '{' (observers+=Observer+) '}' ';')?
    'jpaConfig' jpaConfig=JpaConfig';'
    'serviceConfig' serviceConfig=ServiceConfig';'
    '}';

// Service configuration
ServiceConfig: '{'
    ('observers' '{' (observers+=[Observer]+) '}' ';')?
    ('messageBundles' '{' (messageBundles+=[Localized]+) '}' ';')
    '}';

Observer:
    name=ID '{'
    'type' type=CLASSNAME';'
    'during' during=During';'
    'notifyObserver' notify=Notify';'
    'delegate' className=CLASSNAME';'
    ('qualifier' qualifier=CLASSNAME';')?
    '}'';';

// Jpa configuration
JpaConfig: '{'
    'localizedEnums' '{' (localizedEnums+=[Localized]+) '}'';'
    ('observers' '{' (observers+=[Observer]+) '}' ';')?
    '}';

// Common
Localized:
    name=ID '{'
    ('values' '{' (values+=LocalizedEntry+) '}' ';')?
    '}'';';

LocalizedEntry: '{'
    'key' localizedKey=LOCALIZEDKEY';'
    'values' '{' (values+=LocalizedValue+) '}' ';'
    ('args' '{' (args+=STRING+) '}' ';')?
    '}'';';

LocalizedValue: '{'
    'locale' locale=Locale';'
    'value' value=STRING';'
    '}'';';

// Constants
enum Locale: DE_DE='de_DE' | EN_US='en_US';
enum Boolean: TRUE='true' | FALSE='false';
enum During: IN_PROG='InProgress' | AFTER_COMPLETION='AfterCompleition';
enum Notify: ALWAYS='Always' | EXISTS='Exists';
terminal fragment UAZ: 'A'..'Z';  
terminal fragment LAZ: 'a'..'z';
terminal fragment UAZN: 'A'..'Z'|'0'..'9';  
terminal CLASSNAME: (LAZ+ '.')+ (UAZ LAZ*)+;
terminal LOCALIZEDKEY: UAZN+ ('_' UAZN)*;